apply plugin: 'com.android.library'

android {

    compileSdkVersion config['target-sdk']
    buildToolsVersion config['build-tools']

    defaultConfig {

        minSdkVersion config['min-sdk']
        targetSdkVersion config['target-sdk']
        versionCode 1
        versionName version
    }

    testOptions.unitTests.includeAndroidResources = true
}

dependencies {

    deps.with {
        api it['support-annotations']
    }

    deps.test.with {
        testImplementation it['junit']
        testImplementation it['robolectric']
        testImplementation it['mockito']
    }
}

task javadoc(type: Javadoc) {

    dependsOn 'cleanJavadoc'

    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../docs/")
    title = 'Requirements'
    failOnError false
}

task cleanJavadoc(type: Delete) {
    delete file('../docs')
}

afterEvaluate {
    generateReleaseBuildConfig.enabled = false
    // still cannot find a way o disable generation of empty R file
}

if (project.hasProperty('release')) {
    apply from: config['gradle-push-aar']
}
